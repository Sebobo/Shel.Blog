##
# Extend page type with an additional header data
#
prototype(Neos.Neos:Page) {
    head {
        feedLinks = Neos.Fusion:Collection {
            collection = ${q(site).find('[instanceof Shel.Blog:BlogFeed]')}
            itemName = 'feed'
            iterationName = 'iteration'
            itemRenderer = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel = 'alternate'
                    type = 'application/atom+xml'
                    title = ${feed.properties.title}
                    href = Neos.Neos:NodeUri {
                        node = ${feed}
                        absolute = true
                        format = 'atom'
                    }
                }
            }

            @cache {
                mode = 'cached'
                entryIdentifier {
                    identifier = 'rss-links'
                    workspace = ${node.context.workspace.name}
                }
                entryTags.1 = 'NodeType_Shel.Blog:BlogFeed'
            }
        }

        blogMicroData = Shel.Blog:MicroData {
            @if.isBlogPost = ${q(documentNode).is('[instanceof Shel.Blog:BlogEntry]')}
        }
    }
}
