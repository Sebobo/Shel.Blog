prototype(Shel.Blog:Component.MicroData) < prototype(Neos.Fusion:Component) {
    node = null
    @context.node = ${this.node}

    publisherLogoWidth = 600
    @context.publisherLogoWidth = ${this.publisherLogoWidth}
    publisherLogoHeight = 60
    @context.publisherLogoHeight = ${this.publisherLogoHeight}
    imageWidth = 2000
    @context.imageWidth = ${this.imageWidth}
    imageHeight = 800
    @context.imageHeight = ${this.imageHeight}

    headline = ${q(this.node).property('title')}
    datePublished = ${Date.format(q(this.node).property('publicationDate'), 'c')}
    dateModified = ${Date.format(q(this.node).property('_lastModificationDateTime'), 'c')}
    description = ${String.stripTags(q(this.node).property('entrySummary'))}
    mainEntityOfPage = Neos.Neos:NodeUri {
        node = ${node}
        absolute = true
    }
    author = Neos.Seo:StructuredData.Object {
        type = 'Person'
        renderer.name = ${q(node).property('authorName')}
        @if.hasAuthor = ${!String.isBlank(q(node).property('authorName'))}
    }
    publisher = Neos.Seo:StructuredData.Object {
        type = 'Organization'
        renderer.name = ${site.context.currentSite.name}
        renderer.logo = Neos.Seo:StructuredData.Object {
            type = 'ImageObject'
            renderer.width = ${publisherLogoWidth}
            renderer.height = ${publisherLogoHeight}
            renderer.url = Neos.Neos:ImageUri {
                asset = ${q(node).property('entryImage')}
                width = ${publisherLogoWidth}
                height = ${publisherLogoHeight}
            }
            @if.hasImage = ${q(node).property('entryImage')}
        }
    }
    image = Neos.Seo:StructuredData.Object {
        type = 'ImageObject'
        renderer.width = ${imageWidth}
        renderer.height = ${imageHeight}
        renderer.url = Neos.Neos:ImageUri {
            asset = ${q(node).property('entryImage')}
            width = ${imageWidth}
            height = ${imageHeight}
        }
        @if.hasImage = ${q(node).property('entryImage')}
    }

    renderer = afx`
        <script type="application/ld+json">
            <Neos.Seo:StructuredData.RootObject
                type="NewsArticle"
                renderer.headline={props.data.headline}
                renderer.datePublished={props.data.datePublished}
                renderer.dateModified={props.data.dateModified}
                renderer.description={props.data.description}
                renderer.mainEntityOfPage={props.data.mainEntityOfPage}
                renderer.author={props.data.author}
                renderer.publisher={props.data.publisher}
                renderer.image={props.data.image}/>
        </script>
    `
}
