prototype(Shel.Blog:Component.Article) < prototype(Neos.Neos:ContentComponent) {
    settings = ${Configuration.setting('Shel.Blog')}
    cldrFormat = ${this.settings.cldrFormat}
    localeFormatLength = ${this.settings.localeFormatLength}
    showAuthor = ${this.settings.showAuthor}

    documentNode = ${documentNode}
    node = ${node}
    identifier = ${node.identifier}

    title = ${q(node).property('title')}
    entrySummary = ${q(node).property('entrySummary')}
    categories = ${q(node).property('categories') || []}
    authorName = ${q(node).property('authorName')}
    alternativeText = ${q(node).property('alternativeText')}
    entryImage = ${q(node).property('entryImage')}

    rawPublicationDate = ${q(node).property('publicationDate')}
    publicationDate = ${this.rawPublicationDate}
    publicationDate.@process.fallback = ${value != '' && value != null ? value : q(node).property('_lastPublicationDateTime')}
    publicationDate.@process.format = ${Shel.Blog.Format.date(value, 'Y-m-d', 'date', this.localeFormatLength, this.cldrFormat)}

    firstTextNode = ${q(node).children('main').find('[instanceof Neos.Neos:Content][text]').get(0)}

    summary = ${this.entrySummary != '' && this.entrySummary != null ? this.entrySummary  : (this.firstTextNode ? q(this.firstTextNode).property('text') : '')}
    summary.@process.stripTags = ${String.stripTags(value)}
    summary.@process.stripTags.@if.live = ${!documentNode.context.inBackend}
    summary.@process.crop = ${String.crop(value, 200, 'â€¦')}
    summary.@process.crop.@if.live = ${!documentNode.context.inBackend}

    articleUri = Neos.Neos:NodeUri {
        node = ${node}
        absolute = true
        format = 'html'
    }

    thumbnailWidth = 1280
    thumbnailHeight = 500
    thumbnailAllowCropping = false
    thumbnailAllowUpScaling = false
    thumbnail = ${Shel.Blog.Asset.createThumbnail(this.entryImage, this.thumbnailWidth, null, this.thumbnailHeight, null, this.thumbnailAllowCropping, this.thumbnailAllowUpScaling)}

    renderer = afx`
        <article class="blog-article">
            <header @key="header" class="blog-article__header">
                <figure class="blog-article__image" @key="image" @if.hasImage={props.entryImage}>
                    <Neos.Fusion:Tag tagName="img" @children="attributes.src"
                        attributes.alt={props.alternativeText} attributes.title={props.title}
                        attributes.width={props.thumbnail.width} attributes.height={props.thumbnail.height}>
                        <Neos.Fusion:ResourceUri resource={props.thumbnail.resource}/>
                    </Neos.Fusion:Tag>
                </figure>

                <h1 class="blog-article__headline" @key="headline">
                    <Neos.Neos:Editable property="title" block={false}/>
                </h1>

                <small class="blog-article__author" @key="author" @if.showAuthor={props.showAuthor}>
                    <Neos.Neos:Editable property="authorName" block={false}/>
                </small>

                <time class="blog-article__publication-date" @key="publicationDate">{props.publicationDate}</time>

                <Shel.Blog:Component.CategoryList @key="categoryList" categories={props.categories}/>
            </header>

            <Neos.Neos:ContentCollection @key="content" attributes.class="blog-article__content" nodePath="main"/>

            <footer @key="footer" class="blog-article__footer">
                <Shel.Blog:Component.SocialButtons article={node}/>
                <Shel.Blog:Component.ArticlePager article={node}/>
            </footer>
        </article>
    `
}
