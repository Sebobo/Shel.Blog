##
# This is the content part of our blogfeed.
# It's used for the xml and the normal html output, just uses different templates for the two cases.
#
prototype(Shel.Blog:BlogFeedContent) < prototype(Neos.Neos:Content) {
    templatePath = ${'resource://Shel.Blog/Private/Templates/Content/BlogFeed.' + request.format}
    attributes.class = 'blog-feed'
    sectionName = 'feed'
    configuration = ${Configuration.setting('Shel.Blog')}

    title = ${q(node).property('title')}
    description = ${q(node).property('description')}
    sortingDirection = ${q(node).property('sortingDirection')}
    limit = ${q(node).property('limit')}

    # Make some properties of the node directly accessible in the template to allow inline editing and reduce code.
    sourceNode = ${q(node).property('sourceNode') != null ? q(node).property('sourceNode') : node}
    @context.sourceNode = ${this.sourceNode}
    requestArguments = ${request.pluginArguments}

    # Fetch feed entries
    feedEntries = ${this.sourceNode != null ? q(this.sourceNode).find('[instanceof Shel.Blog:BlogEntry]').get() : []}
    feedEntries.@process {
        sort = ${value ? q(value).sortBlogEntries('publicationDate', this.sortingDirection == 'descending' ? 'DESC' : 'ASC').get() : value}
        slice = ${this.limit > 0 ? Array.slice(value, 0, this.limit) : value}
    }
    @context.feedEntries = ${this.feedEntries}
    feedContent = Neos.Fusion:Collection {
        collection = ${feedEntries}
        itemName = 'node'
        itemRenderer = Shel.Blog:BlogFeedEntry
    }

    # Create condition if paginated feed should be rendered
    itemsPerPage = ${this.configuration.pagination.itemsPerPage}
    maxNumberOfPages = ${Math.ceil(q(this.feedEntries).count() / this.itemsPerPage)}
    renderFeed = ${q(this.feedEntries).count() > 0 && currentBlogPage > 0 && currentBlogPage <= this.maxNumberOfPages}

    # Setup source node to retrieve blog posts from
    lastUpdatedNode = ${this.sortingDirection == 'descending' ? Array.first(feedEntries) : Array.last(feedEntries)}
    lastUpdated = ${q(this.lastUpdatedNode).property('publicationDate')}
    lastUpdated.@process.fallback = ${value != '' && value != null ? value : q(this.lastUpdatedNode).property('_lastPublicationDateTime')}

    # Can be used to add attributes to the list containing the entries in the feed
    listAttributes = Neos.Fusion:Attributes

    @cache {
        mode = 'cached'
        entryIdentifier {
            format = ${request.format}
            node = ${node}
            editPreviewMode = ${node.context.currentRenderingMode.name}
        }
        // Flush the blog feed when a blog entry is changed
        entryTags {
            1 = ${'Node_' + node.identifier}
            2 = ${'Node_' + sourceNode.identifier}
            3 = ${'DescendantOf_' + sourceNode.identifier}
        }
    }
}
