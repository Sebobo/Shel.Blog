prototype(Neos.Neos:Page) {
    @context {
        isBlogEntry = ${q(documentNode).is('[instanceof Shel.Blog:Document.Article]')}
        isBlogFeed = ${q(documentNode).is('[instanceof Shel.Blog:Document.Feed]') && request.format == 'html'}
        currentBlogPage = ${String.toInteger(request.pluginArguments['blog-pagination'].currentPage || 1)}
    }

    head {
        titleTag.content.@process.addBlogPageNumber = Neos.Fusion:Value {
            @if.isPaginating = ${currentBlogPage > 1}
            value = ${value + ' - ' + I18n.translate('Shel.Blog:Main:paginationSuffix') + ' ' + currentBlogPage}
        }

        feedLinks = Neos.Fusion:Collection {
            collection = ${q(site).find('[instanceof Shel.Blog:Document.Feed]')}
            itemName = 'feed'
            iterationName = 'iteration'
            itemRenderer = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel = 'alternate'
                    type = 'application/atom+xml'
                    title = ${feed.properties.title}
                    href = Neos.Neos:NodeUri {
                        node = ${feed}
                        absolute = true
                        format = 'atom'
                    }
                }
            }

            @cache {
                mode = 'cached'
                entryIdentifier {
                    identifier = 'rss-links'
                    workspace = ${node.context.workspace.name}
                }
                entryTags {
                    blogFeeds = ${Neos.Caching.nodeTypeTag('Shel.Blog:Document.Feed')}
                }
            }
        }

        canonicalLink.attributes.href {
            additionalParams = Neos.Fusion:DataStructure {
                '--blog-pagination' = ${{'currentPage': currentBlogPage}}
                '--blog-pagination'.@if.notFirstPage = ${currentBlogPage > 1}
            }
        }

        nextBlogPageLink = Neos.Fusion:Tag {
            tagName = 'link'
            itemsPerPage = ${q(documentNode).property('itemsPerPage') || 5}
            @context.maxNumberOfPages = ${Math.ceil(q(documentNode).children().count() / this.itemsPerPage)}
            attributes {
                rel = 'next'
                href = Neos.Neos:NodeUri {
                    node = ${documentNode}
                    absolute = true
                    additionalParams = Neos.Fusion:DataStructure {
                        '--blog-pagination' = ${{'currentPage': currentBlogPage + 1}}
                    }
                }
            }
            @if.isBlogFeed = ${isBlogFeed}
            @if.notLastPage = ${currentBlogPage < maxNumberOfPages}
        }

        previousBlogPageLink = Neos.Fusion:Tag {
            tagName = 'link'
            attributes {
                rel = 'prev'
                href = Neos.Neos:NodeUri {
                    node = ${documentNode}
                    absolute = true
                    additionalParams = Neos.Fusion:DataStructure {
                        '--blog-pagination' = ${{'currentPage': currentBlogPage - 1}}
                    }
                }
            }
            @if.isBlogFeed = ${isBlogFeed}
            @if.notFirstPage = ${currentBlogPage > 1}
        }

        ampLink = Neos.Fusion:Tag {
            tagName = 'link'
            attributes {
                rel = 'amphtml'
                href = Neos.Neos:NodeUri {
                    node = ${documentNode}
                    absolute = true
                    additionalParams = ${{'--blog-amplify': true}}
                }
            }
            @if.isBlogEntry = ${isBlogEntry}
            @if.notAmp = ${blogAmpMode != true}
        }

        blogMicroData = Shel.Blog:Component.MicroData {
            @if.isBlogEntry = ${isBlogEntry}
        }
    }
}
