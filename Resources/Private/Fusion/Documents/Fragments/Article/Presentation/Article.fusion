prototype(Shel.Blog:Document.Fragment.Article.Presentation) < prototype(Neos.Fusion:Component) {
    title = ''
    image = null
    imageWidth = null
    imageHeight = null
    alternativeText = ''
    authorName = ''
    showAuthor = false
    publicationDate = null
    categories = null
    className = 'blog-article'
    content = ''

    renderer = afx`
        <article class={props.className}>
            <header @key="header" class={props.className + '__header'}>
                <figure class={props.className + '__image'} @key="image" @if.hasImage={props.image}>
                    <img
                        src={props.image}
                        alt={props.alternativeText}
                        width={props.imageWidth}
                        height={props.imageHeight}
                    />
                </figure>

                <h1 class={props.className + '__headline'} @key="headline">{props.title}</h1>

                <small class={props.className + '__author'} @key="author" @if.showAuthor={props.showAuthor}>
                    {props.authorName}
                </small>

                <time class={props.className + '__publication-date'} @key="publicationDate">{props.publicationDate}</time>

                <Shel.Blog:Component.CategoryList @key="categoryList" categories={props.categories}/>
            </header>

            <Shel.Blog:Component.TableOfContents @key="tableOfContents"/>

            <div class={props.className + '__content'} @key="content">
                {props.content}
            </div>

            <footer @key="footer" class={props.className + '__footer'}>
                <Shel.Blog:Component.ShareButtons article={node}/>
                <Shel.Blog:Component.ArticlePager article={node}/>
            </footer>
        </article>
    `
}
