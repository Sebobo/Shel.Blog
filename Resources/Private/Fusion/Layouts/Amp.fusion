prototype(Shel.Blog:AmpVersion) < prototype(Neos.Fusion:Value) {
    value = ${request.pluginArguments['blog-amplify']}
}

prototype(Shel.Blog:AmpPage) < prototype(Neos.Neos:Page) {
    # Additional scripts are not allowed with amp
    lastVisitedNodeScript >

    htmlTag.attributes.amp = true

    head {
        viewportTag = afx`<meta name="viewport" content="width=device-width,minimum-scale=1,initial-scale=1"/>`

        stylesheets = ''
        javascripts >

        ampStylesheets = Neos.Fusion:Join {
            ampBoilerplate = Neos.Fusion:Template {
                templatePath = 'resource://Shel.Blog/Private/Templates/FusionObjects/AmpBoilerplate.html'
            }

            ampBlogStyles = afx`
                <style amp-custom="true">
                    <Neos.Fusion:Template @key="baseStyles"
                        templatePath="resource://Shel.Blog/Private/Styles/Amp.css"/>
                </style>
            `
            @position = 'after titleTag'
        }

        ampScripts = Neos.Fusion:Join {
            main = afx`<script async="" src="https://cdn.ampproject.org/v0.js"></script>`
            @position = 'after ampStylesheets'
        }
    }

    body = Neos.Fusion:Tag {
        attributes.class = 'content'

        javascripts >

        content = Neos.Fusion:Component {
            renderer = Neos.Fusion:Join {
                header = afx`<header class="site-header">{site.context.currentSite.name}</header>`

                breadcrumb = Neos.Fusion:Component {
                    items = ${q(documentNode).parents('[instanceof Neos.Neos:Document][hiddenInIndex != true]').get()}
                    items.@process.removeSitenode = ${Array.pop(value)}
                    items.@process.reverse = ${Array.reverse(value)}

                    renderer = afx`
                        <nav class="breadcrumb">
                            <ol class="breadcrumb__list">
                                <li class="breadcrumb__link">
                                    <Neos.Fusion:Tag tagName="a" content={site.label} @children="attributes.href"
                                        attributes.title={q(site).property('titleOverride') || site.label}>
                                        <Neos.Neos:NodeUri node={site}/>
                                    </Neos.Fusion:Tag>
                                </li>

                                <Neos.Fusion:Loop items={props.items} @children="itemRenderer">
                                    <li class="breadcrumb__link">
                                        <Neos.Fusion:Tag tagName="a" content={item.label} @children="attributes.href"
                                            attributes.title={q(item).property('titleOverride') || item.label}>
                                            <Neos.Neos:NodeUri node={item}/>
                                        </Neos.Fusion:Tag>
                                    </li>
                                </Neos.Fusion:Loop>

                                <li class="breadcrumb__link active">{documentNode.label}</li>
                            </ol>
                        </nav>
                    `
                }

                primaryContent = Neos.Neos:PrimaryContent {
                    @process.replaceImgTags = ${String.pregReplace(value, '/<img (.*?)>/s', '<amp-img layout=responsive $1>')}
                }
            }
        }
    }
}

blogEntryAmp = Shel.Blog:AmpPage

root.blogEntryAmp {
    @position = 'before format'
    @context.blogAmpVersion = Shel.Blog:AmpVersion
    condition = ${!!blogAmpVersion}
    renderPath = '/blogEntryAmp'
}
