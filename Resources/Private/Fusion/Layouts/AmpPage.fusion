prototype(Shel.Blog:Layout.AmpPage) < prototype(Neos.Neos:Page) {
    # Disable Neos.GoogleAnalytics and include amp tag manager -> https://support.google.com/tagmanager/answer/9205783?hl=en
    googleTagManagerNoScript >
    googleTracking >
    googleTrackingScript = Neos.Fusion:Join {
        main = afx`
            <script async custom-element="amp-analytics" src="https://cdn.ampproject.org/v0/amp-analytics-0.1.js"></script>
        `
        @if.isset = ${Configuration.setting('Shel.Blog.analytics.tagManager.id')}
        @position = 'before closingHeadTag'
    }
    googleTrackingCustomElement = Neos.Fusion:Join {
        main = afx`
            <amp-analytics config={'https://www.googletagmanager.com/amp.json?id=' + Configuration.setting('Shel.Blog.analytics.tagManager.id') + '&gtm.url=SOURCE_URL'} data-credentials="include"></amp-analytics>
        `
        @if.isset = ${Configuration.setting('Shel.Blog.analytics.tagManager.id')}
        @position = 'before closingBodyTag'
    }


    # Additional scripts are not allowed with amp
    lastVisitedNodeScript >

    htmlTag.attributes.amp = true

    head {
        viewportTag = afx`<meta name="viewport" content="width=device-width,minimum-scale=1,initial-scale=1"/>`

        stylesheets = ''
        javascripts >

        ampStylesheets = Neos.Fusion:Join {
            ampBoilerplate = Shel.Blog:Component.AmpBoilerplate
            ampBlogStyles = Shel.Blog:Component.AmpStyles
        }

        ampScripts = Neos.Fusion:Join {
            main = afx`<script async="" src="https://cdn.ampproject.org/v0.js"></script>`
            @position = 'after ampStylesheets'
        }
    }

    body = Neos.Fusion:Tag {
        attributes.class = 'content'

        javascripts >

        content = Neos.Fusion:Component {
            renderer = Neos.Fusion:Join {
                header = afx`<header class="site-header">{site.context.currentSite.name}</header>`

                breadcrumb = Neos.Fusion:Component {
                    items = ${q(documentNode).parents('[instanceof Neos.Neos:Document][hiddenInIndex != true]').get()}
                    items.@process.removeSitenode = ${Array.pop(value)}
                    items.@process.reverse = ${Array.reverse(value)}

                    renderer = afx`
                        <nav class="breadcrumb">
                            <ol class="breadcrumb__list">
                                <li class="breadcrumb__link">
                                    <Neos.Fusion:Tag tagName="a" content={site.label} @children="attributes.href"
                                        attributes.title={q(site).property('titleOverride') || site.label}>
                                        <Neos.Neos:NodeUri node={site}/>
                                    </Neos.Fusion:Tag>
                                </li>

                                <Neos.Fusion:Loop items={props.items} @children="itemRenderer">
                                    <li class="breadcrumb__link">
                                        <Neos.Fusion:Tag tagName="a" content={item.label} @children="attributes.href"
                                            attributes.title={q(item).property('titleOverride') || item.label}>
                                            <Neos.Neos:NodeUri node={item}/>
                                        </Neos.Fusion:Tag>
                                    </li>
                                </Neos.Fusion:Loop>

                                <li class="breadcrumb__link active">{documentNode.label}</li>
                            </ol>
                        </nav>
                    `
                }

                primaryContent = Neos.Neos:PrimaryContent {
                    shelBlogArticle {
                        condition = true
                        type = 'Shel.Blog:Component.Article'
                    }

                    # Replace all image tags to make them compatible with AMP
                    @process.replaceImgTags = ${String.pregReplace(value, '/<img (.*?)>/s', '<amp-img layout=responsive $1>')}
                }
            }
        }
    }
}

shelBlogArticleAmp = Shel.Blog:Layout.AmpPage

root.shelBlogArticleAmp {
    @position = 'before format'
    condition = Shel.Blog:Helper.AmpMode
    renderPath = '/shelBlogArticleAmp'
}
