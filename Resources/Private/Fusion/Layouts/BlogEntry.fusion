##
# "BlogEntry" layout
#
prototype(Shel.Blog:BlogEntryContent) < prototype(Neos.Neos:Content) {
    templatePath = 'resource://Shel.Blog/Private/Templates/Content/BlogEntry.html'
    partialRootPath = 'resource://Shel.Blog/Private/Templates/Partials'
    attributes.class = 'blog-entry'
    sectionName = 'blogEntry'
    settings = ${Configuration.setting('Shel.Blog')}
    @context.settings = ${this.settings}

    documentNode = ${documentNode}

    cldrFormat = ${this.settings.cldrFormat}
    localeFormatLength = ${this.settings.localeFormatLength}
    publicationDate = ${q(node).property('publicationDate')}
    publicationDate.@process.fallback = ${value != '' && value != null ? value : q(node).property('_lastPublicationDateTime')}

    showAuthor = ${this.settings.showAuthor}
    authorName = ${q(node).property('authorName')}

    title = ${q(node).property('title')}
    entrySummary = ${q(node).property('entrySummary')}

    firstTextNode = ${q(node).children('main').find('[instanceof Neos.NodeTypes:Text]').get(0)}
    summary = ${this.entrySummary != '' && this.entrySummary != null ? this.entrySummary  : (this.firstTextNode ? q(this.firstTextNode).property('text') : '')}
    summary.@process {
        stripTags = ${String.stripTags(value)}
        crop = ${String.crop(value, 200, 'â€¦')}
    }

    entryUrl = Neos.Neos:NodeUri {
        node = ${documentNode}
        absolute = true
    }
    @context.entryUrl = ${this.entryUrl}

    pagerArguments = Neos.Fusion:Case {
        default.condition = ${true}
        default.renderer = ${[]}
        default.@position = 'end'

        amp.condition = ${blogAmpVersion == true}
        amp.renderer = Neos.Fusion:RawArray {
            '--blog-amplify' = true
        }
    }

    content = Neos.Neos:ContentCollection {
        attributes.class = 'blog-entry__content'
        nodePath = 'main'
    }

    imageSettings = Neos.Fusion:RawArray {
        class = 'blog-entry-image'
        width = 150
        height = 150
        cropping = false
        upScaling = false
    }

    socialButtons = Shel.Blog:SocialButtons {
        title = ${q(documentNode).property('title')}
        entryUrl = ${entryUrl}
    }

    disqus = Shel.Blog:Disqus {
        identifier = ${documentNode.identifier}
        title = ${q(documentNode).property('title')}
        entryUrl = ${entryUrl}
        @if.notAmp = ${blogAmpVersion != true}
    }

    alternativeText = ${q(node).property('alternativeText')}
    entryImage = ${q(node).property('entryImage')}

    previousEntry = ${q(node).prev('[instanceof Shel.Blog:BlogEntry]').get(0)}
    nextEntry = ${q(node).next('[instanceof Shel.Blog:BlogEntry]').get(0)}
}

##
# Add a condition to render the primary content differently if we show a blog entry
#
prototype(Neos.Neos:PrimaryContent).BlogEntry {
    condition = ${q(node).is('[instanceof Shel.Blog:BlogEntry]')}
    type = 'Shel.Blog:BlogEntryContent'
}
